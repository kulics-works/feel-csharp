# Compiler

# {
    Antlr4.Runtime
    System
    System.IO
    System.Text
}

_read_path: Str
_path_line: Str

Main = (args: Array[Str]->) {
    os = Environment.OSVersion.Platform
    ? os == PlatformID.Unix || os == PlatformID.MacOSX {
        _read_path = "./"
        _path_line = "/"
    } | {
        _read_path = ".\\"
        _path_line = "\\"
    }
    Compiled(_read_path)
    Print("Completed")
}

Compiled = (path: Str->) {
    Files = Directory.GetFiles(path, "*.feel")
    @ file = Files... {
        ! fsRead = FileStream$(file, FileMode.Open); fsRead {
            FSLength = fsRead.Length.[Int]
            ByteBlock = Array[U8](FSLength, (i){0})
            r = fsRead.Read(ByteBlock, 0, ByteBlock.Length)
            Input = Encoding.UTF8.GetString(ByteBlock)

            Stream = AntlrInputStream$(Input)
            Lexer = FeelLexer$(Stream)
            Tokens = CommonTokenStream$(Lexer)
            Parser = FeelParser$(Tokens)
            Parser.BuildParseTree = true
            Parser.RemoveErrorListeners()
            Parser.AddErrorListener(ErrorListener$(file))

            AST = Parser.program()

            Visitor = FeelLangVisitor$()
            Result = Visitor.Visit(AST)

            ByteResult = Encoding.UTF8.GetBytes(Result.To_Str())  
            ! fsWrite = FileStream$("\{_read_path, file.Replace(".feel", ".cs")}", FileMode.Create); fsWrite {
                fsWrite.Write(ByteResult, 0, ByteResult.Length)
            }
        }
    }

    @ folder = Directory.GetDirectories(path)... {
        Compiled(folder)
    }
}
